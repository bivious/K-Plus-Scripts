################################################################################
#
#   This file is part of the PM Manager Addon
#   Copyright (C) 2017 slaboff (slaboff at no mail dot not)
#
#   This program is FREE software. You can redistribute it and/or
#   modify it under the terms of the GNU General Public License
#   as published by the Free Software Foundation; either version 2
#   of the License, or (at your option) any later version.
#
#   This program is distributed in the HOPE that it will be USEFUL,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
#   See the GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program. If not, write to the Free Software Foundation,
#   Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
#
################################################################################

event(OnKVIrcStartup,blockPM)
{
	%mywhitelist = $file.localdir("mywhitelist.txt")
	%inFile = %mywhitelist
	%conf = $config.open(%inFile, r)
	config.setsection %conf whitelist
	%whitelist = $config.keylist(%conf)

	foreach(%i, %whitelist)
	{
		%G_pmWhiteList[%i] = $config.read(%conf, %i)
	}

	config.close %conf
	%pmoptions = $config.open(QUPMM.kvc)
	%G_pmReplyMsg = $config.read(%pmoptions, PMMSG)
	%G_pmPMLog = $config.read(%pmoptions, PMLOG)
	config.close %pmoptions
}

event(OnQueryMessage,blockPM)
{
	if((%G_pmTmpUser != $null) && (%G_pmTmpUser == $0))
	{
		return;
	}

	if($my.nick == $0)
	{
		return;
	}

	foreach(%user, %G_pmWhiteList)
	{
		if($0 == %user)
		{
			return;
		}
	}

	%noticemsg = ""$b"Automated Message:"$b" "$u"Uninvited PM's"$u" are not being accepted!"
	if(%G_pmReplyMsg != $false)
	{
		%noticemsg = %G_pmReplyMsg
	}

	%BlockedNick = $0
	%closeit = $new(qupm::pmcloser)
	%winid = "Blocked PM"
	notice $0 %noticemsg

	if(%G_pmPMLog == $true)
	{
		if (!$window.exists($window(%winid)))
		{
			$window.open("m", %winid, $context,203)
		}
		echo -w=$window(%winid) -i=$msgtype(ignore) "$b"$date(F) "BLOCKED PM:"$b $k(0) [ User: $0 ] * Message: $3
	}

	%winclosed = %closeit->$closePMWin(%BlockedNick)
	if(%winclosed == $true)
	{
		unset %BlockedNick
		unset %closeit
	}
	halt;
}
